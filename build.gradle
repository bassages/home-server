plugins {
    id 'org.springframework.boot' version '1.5.7.RELEASE'
    id "com.moowork.gulp" version "1.2.0"
    id 'com.github.ksoichiro.build.info' version '0.2.0'
    id 'com.github.ben-manes.versions' version '0.15.0'
}

if (!project.hasProperty("buildtype")) {
    ext.buildtype = 'dev'
}

buildInfo {
    // Date format string used to Git committer date.
    committerDateFormat "yyyy-MM-dd'T'HH:mm:ssZ"

    // Date format string used to build date.
    buildDateFormat "yyyy-MM-dd'T'HH:mm:ssZ"
}

repositories {
    jcenter()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}

node {
    // Version of node to use.
    version = '6.11.3'
    download = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

version = '1.0.0'

springBoot  {
    // Generates a build-info.properties file
    buildInfo()
}

jar {
    baseName = 'home-server'
}

dependencies {
    compile("org.springframework.boot:spring-boot-starter-security")
    compile("org.springframework.boot:spring-boot-starter-cache")
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-websocket")

    // Needed to make the actuator hypermedia-based “discovery page” for the other endpoints work
    compile("org.springframework.boot:spring-boot-starter-hateoas")

    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-actuator-docs")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-tomcat")
    compile("org.springframework.boot:spring-boot-devtools")

    compile("net.sf.ehcache:ehcache")
    compile("javax.el:javax.el-api:2.2.4")
    compile("commons-io:commons-io:2.5")
    compile("org.apache.commons:commons-collections4:4.1")
    compile("org.apache.commons:commons-lang3:3.6")
    compile("org.apache.httpcomponents:httpclient:4.5.2")

    testCompile("org.springframework.boot:spring-boot-starter-test")

    runtime("com.h2database:h2")
    runtime("mysql:mysql-connector-java")
}

// Take over system properties.
// Needed to be able to set the active profile for Spring Boot.
// e.g. -Dspring.profiles.active=development-h2db
bootRun {
    systemProperties System.properties
}

// make sure gulp generates artifacts for the build
bootRun.dependsOn 'buildGulpFromGradle'
processResources.dependsOn 'buildGulpFromGradle'

// Cleans up node_modules and nodejs dirs on clean task
clean.doFirst {
    delete "${rootDir}/node_modules"
    delete "${rootDir}/.gradle/nodejs"
}

task buildGulpFromGradle(type: GulpTask) {
    description = "Triggers the Gulp build. Adding -Pbuildtype=prod to the gradle command will build frontend resources minified and uglified."

    // Force npmInstall to run (for some reason it won't run after the first clean build, it reports UP-TO-DATE)
    npmInstall.outputs.upToDateWhen { false }

    // make sure npm install has been performed before calling the build task in gulpfile.js
    dependsOn = [ npmInstall ]

    args = ['build', "--buildtype=${project.buildtype}" ]

    // limit memory usage to be able to build in OpenShift container
    options = ['--max-old-space-size=512']
}
