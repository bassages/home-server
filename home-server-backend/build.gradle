plugins {
    id 'java'
    id 'org.springframework.boot' version '2.0.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.5.RELEASE'
    id 'com.moowork.gulp' version '1.2.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'jacoco'
    id "org.sonarqube" version '2.6.2'
}

jacoco {
    toolVersion = '0.8.0'
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.junit.reportsPath", "build/test-results"
        property "sonar.jacoco.reportPath", "build/test-reports/jacoco.exec"

        // Exclude development utils
        property "sonar.coverage.exclusions", "**/dev/**/*.java"
    }
}

if (!project.hasProperty("buildtype")) {
    ext.buildtype = 'dev'
}

repositories {
    jcenter()
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

node {
    // Version of node to use.
    version = '8.10.0' // LTS version
    download = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
}

compileTestJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
}

version = '1.0.0'

jar {
    baseName = 'home-server'
}

dependencies {
    annotationProcessor("org.projectlombok:lombok:1.16.20")
    compileOnly("org.projectlombok:lombok:1.16.20")

    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-cache')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-websocket')
    compile("org.springframework.boot:spring-boot-starter-thymeleaf")

    // Needed to make the actuator hypermedia-based “discovery page” for the other endpoints work
    compile('org.springframework.boot:spring-boot-starter-hateoas')

    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-starter-tomcat')
    compile('org.springframework.boot:spring-boot-devtools')

    compile('net.sf.ehcache:ehcache')
    compile('commons-io:commons-io:2.6')
    compile('org.apache.commons:commons-collections4:4.1')
    compile('org.apache.commons:commons-lang3:3.7')
    compile('org.apache.httpcomponents:httpclient:4.5.5')
    compile('org.apache.commons:commons-math3:3.6.1')
    compile('javax.inject:javax.inject:1')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.assertj:assertj-core:3.9.1')
    testCompile('nl.jqno.equalsverifier:equalsverifier:2.4.5');

    runtime('com.h2database:h2')
    runtime('org.hibernate:hibernate-java8')
    runtime('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
}

springBoot {
    buildInfo()
}

// Take over system properties.
// Needed to be able to set the active profile for Spring Boot.
// e.g. -Dspring.profiles.active=development-h2db
bootRun {
    systemProperties System.properties
}

// Cleans up node_modules and nodejs dirs on clean task
clean.doFirst {
    delete "${rootDir}/node_modules"
    delete "${rootDir}/.gradle/nodejs"
}

task buildAngularJsApplication(type: GulpTask) {
    description = 'Triggers Gulp to build the AngularJS application. ' +
                  'Adding -Pbuildtype=prod to the gradle command will build frontend resources minified and uglified.'

    // Force npmInstall to run (for some reason it won't run after the first clean build, it reports UP-TO-DATE)
    npmInstall.outputs.upToDateWhen { false }

    // make sure npm install has been performed before calling the build task in gulpfile.js
    dependsOn = [ npmInstall ]

    args = ['build', "--buildtype=${project.buildtype}" ]

    // limit memory usage
    options = ['--max-old-space-size=512']
}

// make sure that Gulp generates frontend artifacts
bootRun.dependsOn 'buildAngularJsApplication'
processResources.dependsOn 'buildAngularJsApplication'

processResources.dependsOn getRootProject().project('home-server-frontend')
                                           .getTasksByName('buildAngularApplication', false)[0]
processResources.doLast {
    def frontendArtifacts = '../home-server-frontend/dist'
    def resourcesDirectory = 'build/resources/main/public'

    println '-----------------------------------------------------------------------------------'
    println 'Copy front-end artifacts'
    println 'From: [' + frontendArtifacts + '] To: [' + resourcesDirectory + ']'
    println '-----------------------------------------------------------------------------------'

    copy {
        from frontendArtifacts
        into resourcesDirectory
        rename('index.html', 'ng5.html')
    }
}

test {
    exclude '**/*IntegrationTest.*'
}
