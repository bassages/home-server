plugins {
    id 'java'
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.github.ben-manes.versions' version '0.29.0'
    id 'jacoco'
    id "org.sonarqube" version '3.0'
}

jacoco {
    toolVersion = '0.8.5'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.enabled false
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.junit.reportsPath", "build/test-results"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"
    }
}

repositories {
    jcenter()
}

java {
    sourceCompatibility = JavaVersion.VERSION_14
    targetCompatibility = JavaVersion.VERSION_14
}

compileJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
    options.encoding = 'UTF-8'
}

if (System.getenv("TRAVIS_TAG") != null) {
    version = System.getenv("TRAVIS_TAG")
} else {
    version = 'x.x.x'
}

jar {
    archiveBaseName = 'home-server'
}

dependencies {
    annotationProcessor('org.projectlombok:lombok:1.18.12')
    compileOnly('org.projectlombok:lombok:1.18.12')

    implementation('org.springframework.boot:spring-boot-starter-security')
    implementation('org.springframework.boot:spring-boot-starter-cache')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-websocket')

    implementation('org.springframework.boot:spring-boot-starter-validation')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')
    implementation('org.springframework.boot:spring-boot-starter-tomcat')
    compileOnly('org.springframework.boot:spring-boot-devtools')

    implementation('net.sf.ehcache:ehcache')
    implementation('commons-io:commons-io:2.7')
    implementation('org.apache.commons:commons-collections4:4.4')
    implementation('org.apache.commons:commons-lang3:3.11')
    implementation('org.apache.httpcomponents:httpclient:4.5.12')
    implementation('org.apache.commons:commons-math3:3.6.1')
    implementation('javax.inject:javax.inject:1')
    implementation('javax.xml.bind:jaxb-api:2.3.0')
    implementation('com.google.code.findbugs:jsr305:3.0.2')

    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation('org.assertj:assertj-core:3.16.1')
    testImplementation('nl.jqno.equalsverifier:equalsverifier:3.4.1')

    runtimeOnly('com.h2database:h2')
    runtimeOnly('org.hibernate:hibernate-java8')
    runtimeOnly('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
}

springBoot {
    buildInfo()
}

// Take over system properties.
// Needed to be able to set the active profile for Spring Boot.
// e.g. -Dspring.profiles.active=development-h2db
bootRun {
    systemProperties System.properties
}

def recursive = false
processResources.dependsOn getRootProject().project('home-server-frontend')
                                           .getTasksByName('buildAngularApplication', recursive)[0]
processResources.doLast {
    def frontendArtifacts = '../home-server-frontend/dist'
    def resourcesDirectory = 'build/resources/main/static'

    println '-----------------------------------------------------------------------------------'
    println 'Copy front-end artifacts'
    println 'From: [' + frontendArtifacts + '] To: [' + resourcesDirectory + ']'
    println '-----------------------------------------------------------------------------------'

    copy {
        from frontendArtifacts
        into resourcesDirectory
    }
}

test {
    exclude '**/*IntegrationTest.*'
}
